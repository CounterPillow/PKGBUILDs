# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org
# Contributor: Justin Settle <jus10 at partlycloudy dot org>

pkgname=vboot-utils
pkgver=r3132.c4102fe4e
pkgrel=1
arch=('armv7h' 'aarch64')
pkgdesc="Chromium OS vboot utilities"
url="https://chromium.googlesource.com/chromiumos/platform/vboot_reference/+/master"
license=('custom:chromiumos')
makedepends=('libyaml' 'git')
depends=('bash')
provides=('cgpt')
replaces=('cgpt')
source=("git+https://chromium.googlesource.com/chromiumos/platform/vboot_reference")
md5sums=('SKIP')

pkgver() {
  cd vboot_reference
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd vboot_reference
  # remove -Werror to prevent build failure
  sed -i 's/-Werror//g' Makefile
}

build() {
  cd vboot_reference
  ARCH=arm make V=1 USE_FLASHROM=0
}

package() {
  cd vboot_reference
  make DESTDIR="${pkgdir}" USE_FLASHROM=0 install
}
